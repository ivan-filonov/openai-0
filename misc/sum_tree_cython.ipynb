{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cython"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython -c=-O3\n",
    "# cython: boundscheck=False\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "\n",
    "from cython.parallel cimport prange\n",
    "from libc.math cimport pow, exp, log\n",
    "\n",
    "cdef int _st_get_one_tree_idx(float[:] tree, int capacity, float s) nogil:\n",
    "    cdef int parent = 0\n",
    "    cdef int base = capacity - 1\n",
    "    cdef float lval\n",
    "    while parent < base:\n",
    "        parent = parent * 2 + 1\n",
    "        lval = tree[parent]\n",
    "        if s > lval and tree[parent+1] > 0:\n",
    "            s -= lval\n",
    "            parent += 1\n",
    "    return parent\n",
    "\n",
    "cdef void _st_to_prios(float[:] errors, float upper, float alpha) nogil:\n",
    "    cdef int n = errors.shape[0]\n",
    "    cdef int i\n",
    "    cdef float e\n",
    "    for i in range(n):\n",
    "        e = errors[i]\n",
    "        if e < 1e-5:\n",
    "            e = 1e-5\n",
    "        if e > upper:\n",
    "            e = upper\n",
    "        errors[i] = pow(e, alpha)\n",
    "\n",
    "cdef void _st_update_one(float[:] tree, int capacity, int data_idx, float value) nogil:\n",
    "    cdef int i = data_idx + capacity - 1\n",
    "    cdef float d = value - tree[i]\n",
    "    while True:\n",
    "        tree[i] += d\n",
    "        if 0 == i:\n",
    "            break\n",
    "        i = (i - 1) // 2\n",
    "\n",
    "cdef void _st_update_many(float[:] tree, int capacity, int[:] data_indices, float[:] values) nogil:\n",
    "    cdef int N = data_indices.shape[0]\n",
    "    cdef int i\n",
    "    for i in range(N):\n",
    "        _st_update_one(tree, capacity, data_indices[i], values[i])\n",
    "\n",
    "cdef void _st_sample(float[:] tree, int capacity, int n, float[:] in_s, int[:] out_indices, float[:] out_weights):\n",
    "    cdef int i\n",
    "    cdef int base = capacity - 1\n",
    "    assert tree.shape[0] == capacity * 2 - 1, \"capacity does not match tree shape\"\n",
    "    assert n == in_s.shape[0], \"in_s shape don't match n\"\n",
    "    assert n == out_indices.shape[0], \"out_indices shape don't match n\"\n",
    "    assert n == out_weights.shape[0], \"out_weights shape don't match n\"\n",
    "    cdef int t_i\n",
    "    for i in prange(n, nogil=True):\n",
    "        t_i = _st_get_one_tree_idx(tree, capacity, in_s[i])\n",
    "        out_indices[i] = t_i - base\n",
    "        out_weights[i] = tree[t_i]\n",
    "\n",
    "class PrioritizedBuffer(object):\n",
    "    def __init__(self, capacity, alpha=0.6, beta=0.4, beta_step=1e-4):\n",
    "        self.capacity = capacity\n",
    "        self.alpha = alpha\n",
    "        self.beta = beta\n",
    "        self.beta_step = beta_step\n",
    "        \n",
    "        self.tree = np.zeros((capacity * 2 - 1,), dtype=np.float32)\n",
    "        self.data = np.empty(capacity, dtype=object)\n",
    "        self.pos = 0\n",
    "        self.count = 0\n",
    "\n",
    "    def __len__(self):\n",
    "        return self.count\n",
    "    \n",
    "    def add(self, obj):\n",
    "        self.data[self.pos] = obj\n",
    "        _st_update_one(self.tree, self.capacity, self.pos, 1)\n",
    "        self.pos = (self.pos + 1) % self.capacity\n",
    "        if self.count < self.capacity:\n",
    "            self.count += 1\n",
    "    \n",
    "    def update(self, indices, weights):\n",
    "        _st_to_prios(weights, 1, self.alpha)\n",
    "        _st_update_many(self.tree, self.capacity, indices, weights)\n",
    "    \n",
    "    def sample(self, n):\n",
    "        weights = np.empty(n, dtype=np.float32)\n",
    "        indices = np.empty(n, dtype=np.int32)\n",
    "        segment = self.tree[0] / n\n",
    "        a = np.arange(n, dtype=np.float32) * segment\n",
    "        s = np.random.uniform(a, a + segment).astype(np.float32)\n",
    "        _st_sample(self.tree, self.capacity, n, s, indices, weights)\n",
    "        self.beta = min(1, self.beta + self.beta_step)\n",
    "        weights = np.power(weights / self.tree[0] * self.count, -self.beta)\n",
    "        weights /= weights.max()\n",
    "        return indices, weights.astype(np.float32), self.data[indices]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "%%time\n",
    "from tqdm import tqdm_notebook\n",
    "p = PrioritizedBuffer(1000000)\n",
    "tq = tqdm_notebook(range(10000000))\n",
    "for k in tq:\n",
    "    p.add(f'_{k}')\n",
    "    if len(p) >= 32:\n",
    "        i, w, d = p.sample(32)\n",
    "        for j in range(32):\n",
    "            assert d[j] is not None, f'k={k}, i[{j}]={i[j]}, w[{j}]={w[j]}, d[{j}]={d[j]}, tree[0]={p.tree[0]}'\n",
    "        w += (np.random.random_sample(32) - 0.5) * 0.5\n",
    "        p.update(i, w)\n",
    "print(i)\n",
    "print(w)\n",
    "print(d)\n",
    "print(p.tree[:15])"
   ]
  },
  {
   "cell_type": "raw",
   "metadata": {},
   "source": [
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
